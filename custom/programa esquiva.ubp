module main
author unknown
version 1 0 
description ''
variables izda distancia 

script 193 40 {
whenStarted
forever {
  distancia = (cutebot_distance)
  adelante 'adelante' '4'
  if (distancia <= 20) {
    adelante 'parar' 0
    izda = (random 0 1)
    adelante 'atrás' '4'
    waitMillis 500
    if (izda == 1) {
      adelante 'izquierda' '4'
      waitMillis 500
    } else {
      adelante 'derecha' '4'
      waitMillis 500
    }
  }
}
}


module AILens
author wwj718
version 1 0 
choices modeMenu Card Ball Color Face Tracking Learn 
description 'AI Lens

The AI Lens is able to achieve the functions of face recognition, balls tracking, cards recognition and characteristics acquisition.

ref: https://www.elecfreaks.com/learn-en/microbitplanetX/ai/Plant_X_EF05035.html
'
variables _Camera_Add _Card _Face _Ball _Tracking _Color _Learn _numberCards _letterCards _otherCards _colorList '_AILens initialized' _Data_buff _Temp_Data_buff _recursion_depth 

  spec ' ' '_AILens setup' '_AILens setup'
  spec ' ' '_get_image' '_get_image'
  spec ' ' 'switch_function' 'switch mode _' 'menu.modeMenu' 'Card'
  spec 'r' 'get_card_content' 'get_card_content'
  spec 'r' 'get_card_data' 'get_card_data'
  spec 'r' 'get_ball_color' 'get_ball_color'
  spec 'r' 'get_ball_data' 'get_ball_data'
  spec 'r' 'get_color_name' 'get_color_name'
  spec 'r' 'get_face' 'get_face'
  spec 'r' 'get_face_data' 'get_face_data'
  spec 'r' 'get_track_data' 'get_track_data'
  spec ' ' 'learn_object' 'learn_object _' 'menu.range:1-5' 1
  spec 'r' 'get_learn_data' 'get_learn_data'
  spec 'r' '_get_7_data' '_get_7_data'

to '_AILens setup' {
  if ((v '_AILens initialized') == 0) {
    _Camera_Add = (hexToInt '14')
    _Card = 0
    _Face = 0
    _Ball = 7
    _Tracking = 1
    _Color = 0
    _Learn = 0
    _numberCards = ('[data:makeList]' 0 1 2 3 4 5 6 7 8 9)
    _letterCards = ('[data:makeList]' 'A' 'B' 'C' 'D' 'E')
    _otherCards = ('[data:makeList]' 'Mouse' 'micro:bit' 'Ruler' 'Cat' 'Peer' 'Ship' 'Apple' 'Car' 'Pan' 'Dog' 'Umbrella' 'Airplane' 'Clock' 'Grape' 'Cup' 'Turn left' 'Turn right' 'Forward' 'Stop' 'Back')
    _colorList = ('[data:makeList]' 'Green' 'Blue' 'Yellow' 'Black' 'Red' 'White')
    _Temp_Data_buff = (newList 9)
    _Data_buff = (newList 9)
    _recursion_depth = 0
    local 'tmp' (newList 1)
    '[sensors:i2cRead]' _Camera_Add tmp
    '_AILens initialized' = 1
  }
}

to '_get_7_data' {
  '_get_image'
  comment 'return (a list):Data [x,y,w,h,confidence,total,order]'
  local 'data' ('[data:makeList]')
  for i 7 {
    '[data:addLast]' (at (i + 2) _Data_buff) data
  }
  return data
}

to '_get_image' {
  '_AILens setup'
  '[sensors:i2cRead]' _Camera_Add _Temp_Data_buff
  if (_Temp_Data_buff == _Data_buff) {
    waitMillis 100
    if (_recursion_depth < 5) {
      _recursion_depth = (_recursion_depth + 1)
      '_get_image'
    }
  } else {
    _Data_buff = _Temp_Data_buff
  }
}

to get_ball_color {
  '_get_image'
  if ((at 1 _Data_buff) == 7) {
    if ((at 2 _Data_buff) == 1) {
      return 'Blue'
    } else {
      if ((at 2 _Data_buff) == 2) {return 'Red'}
    }
  }
  if else {
    return '"No Ball"'
  }
}

to get_ball_data {
  return ('_get_7_data')
}

to get_card_content {
  '_get_image'
  if ((at 1 _Data_buff) == 2) {
    return (at (at 2 _Data_buff) _numberCards)
  }
  if ((at 1 _Data_buff) == 4) {
    return (at (at 2 _Data_buff) _letterCards)
  }
  if (and ((at 1 _Data_buff) == 3) ((at 2 _Data_buff) < 21)) {
    return (at (at 2 _Data_buff) _otherCards)
  }
  return 'No Card'
}

to get_card_data {
  return ('_get_7_data')
}

to get_color_name {
  '_get_image'
  if ((at 1 _Data_buff) == 9) {
    return (at (at 2 _Data_buff) _colorList)
  } else {
    return 'No Color'
  }
}

to get_face {
  '_get_image'
  return ((at 1 _Data_buff) == 6)
}

to get_face_data {
  return ('_get_7_data')
}

to get_learn_data {
  comment 'return: LearnData [ID,confidence]'
  '_get_image'
  return ('[data:makeList]' (at 2 _Data_buff) (100 - (at 3 _Data_buff)))
}

to get_track_data {
  comment 'return: LineData [angel,width,len]'
  '_get_image'
  local 'data' 0
  for i 3 {
    '[data:addLast]' (at (i + 2) _Data_buff) data
  }
  return data
}

to learn_object id {
  '_AILens setup'
  if (or (id > 5) (id < 1)) {
    sayIt '"Learn id out of range"'
  } else {
    '[sensors:i2cWrite]' _Camera_Add ('[data:asByteArray]' ('[data:makeList]' 10 id))
  }
}

to switch_function mode {
  '_AILens setup'
  comment 'mode'
  if (mode == 'Card') {
    mode = 2
  }
  if (mode == 'Face') {
    mode = 6
  }
  if (mode == 'Ball') {
    mode = 7
  }
  if (mode == 'Tracking') {
    mode = 8
  }
  if (mode == 'Color') {
    mode = 9
  }
  if (mode == 'Learn') {
    mode = 10
  }
  '[sensors:i2cWrite]' _Camera_Add ('[data:asByteArray]' ('[data:makeList]' (hexToInt '20') mode))
}


module Cutebot Output
author wwj718
version 1 6 
depends 'Distance (HC-SR04)' NeoPixel 
choices cutebot_side 'cutebotside;left' 'cutebotside;right' 
choices cutebot_light 'cutebotlight;left' 'cutebotlight;right' 'cutebotlight;both' 
description 'Cutebot is a rear-drive smart car driven by dual high speed motors.

It features two RGB headlights, left and right line-following sensors, an ultrasonic distance sensor, and two downward-facing NeoPixels.

Add the IR Remote library to use the IR receiver (pin 16).

The Cutebot can work with the AILens library.

https://www.elecfreaks.com/learn-en/microbitKit/smart_cutebot/cutebot_car.html'

  spec ' ' 'cutebot_setWheelSpeed' 'Cutebot set wheel left _ right _ (-100 to 100)' 'num num' 100 -100
  spec ' ' 'cutebot_stopWheels' 'Cutebot stop wheels'
  space
  spec ' ' 'cutebot_setServo' 'Cutebot set servo _ to _ degrees (-90 to 90)' 'menu.range:1-2 num' 1 90
  spec ' ' 'cutebot_setServoSpeed' 'Cutebot set servo _ to speed _ (-100 to 100)' 'menu.range:1-2 num' 1 100
  space
  spec ' ' 'cutebot_setHeadlightColor' 'Cutebot set headlight _ to _' 'menu.cutebot_light color' 'cutebotlight;both'
  spec ' ' 'cutebot_setNeopixels' 'Cutebot set NeoPixel _ to _' 'menu.cutebot_light color' 'cutebotlight;both'
  space
  spec 'r' 'cutebot_lineSensor' 'Cutebot _ line sensor' 'menu.cutebot_side' 'cutebotside;left'
  spec 'r' 'cutebot_seesLine' 'Cutebot sees line on left _ right _' 'bool bool' true true
  spec 'r' 'cutebot_distance' 'Cutebot distance (cm)'
  space
  spec ' ' '_cutebot_sendCmd' '_cutebot_sendCmd _' 'str' 'aList'

to '_cutebot_sendCmd' aList {
  local 'cutebotAddr' 16
  '[sensors:i2cSetClockSpeed]' 100000
  '[sensors:i2cWrite]' cutebotAddr aList
  '[sensors:i2cSetClockSpeed]' 400000
}

to cutebot_distance {
  local 'trigger_pin' 8
  local 'echo_pin' 12
  return ('distance (cm)' trigger_pin echo_pin)
}

to cutebot_lineSensor side {
  comment 'Return true if a black line is detected on the given side.'
  local 'left_pin' 13
  local 'right_pin' 14
  if (side == 'cutebotside;left') {
    return (not (digitalReadOp left_pin))
  } else {
    return (not (digitalReadOp right_pin))
  }
}

to cutebot_seesLine leftSide rightSide {
  return (and (leftSide == (cutebot_lineSensor 'cutebotside;left')) (rightSide == (cutebot_lineSensor 'cutebotside;right')))
}

to cutebot_setHeadlightColor which color {
  local 'r' ((color >> 16) & 255)
  local 'g' ((color >> 8) & 255)
  local 'b' ((color >> b) & 255)
  local 'left_LED' 4
  local 'right_LED' 8
  if (which == 'cutebotlight;left') {
    '_cutebot_sendCmd' ('[data:makeList]' left_LED r g b)
  } (which == 'cutebotlight;right') {
    '_cutebot_sendCmd' ('[data:makeList]' right_LED r g b)
  } else {
    '_cutebot_sendCmd' ('[data:makeList]' left_LED r g b)
    '_cutebot_sendCmd' ('[data:makeList]' right_LED r g b)
  }
}

to cutebot_setNeopixels which_NeoPixel color {
  if (0 == _np_pixels) {neoPixelAttach 2 15}
  if (which_NeoPixel == 'cutebotlight;left') {
    atPut 1 _np_pixels color
  } (which_NeoPixel == 'cutebotlight;right') {
    atPut 2 _np_pixels color
  } else {
    atPut 1 _np_pixels color
    atPut 2 _np_pixels color
  }
  '_NeoPixel_update'
}

to cutebot_setServo servo value {
  comment 'servo (number) is one of: 1 2
value range of -90 to 90 maps to the full range of the servo'
  servo = (maximum 1 (minimum servo 2))
  value = (maximum -90 (minimum value 90))
  '_cutebot_sendCmd' ('[data:makeList]' (servo + 4) ('[misc:rescale]' value 90 -90 0 180) 0 0)
}

to cutebot_setServoSpeed servo value {
  comment 'servo (number) is one of: 1 2
value range of -100 to 100 maps to the full range of the servo'
  servo = (maximum 1 (minimum servo 2))
  value = (maximum -100 (minimum value 100))
  '_cutebot_sendCmd' ('[data:makeList]' (servo + 4) ('[misc:rescale]' value 100 -100 0 180) 0 0)
}

to cutebot_setWheelSpeed left_wheel_speed right_wheel_speed {
  comment 'speed (number): -100-100
direction: 1 = backward, 2 = forward'
  local 'left_direction' 2
  local 'right_direction' 2
  if (left_wheel_speed < 0) {
    left_direction = 1
    left_wheel_speed = (0 - left_wheel_speed)
  }
  if (right_wheel_speed < 0) {
    right_direction = 1
    right_wheel_speed = (0 - right_wheel_speed)
  }
  left_wheel_speed = (minimum left_wheel_speed 100)
  right_wheel_speed = (minimum right_wheel_speed 100)
  '_cutebot_sendCmd' ('[data:makeList]' 1 left_direction left_wheel_speed 0)
  '_cutebot_sendCmd' ('[data:makeList]' 2 right_direction right_wheel_speed 0)
}

to cutebot_stopWheels {
  cutebot_setWheelSpeed 0 0
}


module 'Distance (HC-SR04)' Input
author 'Joan Guillén & Josep Ferràndiz'
version 1 4 
tags sensor 'hc-sr04' distance ultrasound 
description 'Support for the HC-SR04 ultrasound distance sensor.'
variables _sr04_last 

  spec 'r' 'distance (cm)' 'distance (cm) trigger _ echo _' 'num num' 2 4
  spec 'r' 'sr04_distanceOnePin' 'distance (cm) pin _' 'num' 0

to 'distance (cm)' trig ech {
  comment 'Contributed by Joan Guillén and Josep Ferràndiz'
  digitalWriteOp trig false
  waitMicros 2
  digitalWriteOp trig true
  waitMicros 50
  digitalWriteOp trig false
  local 'start' (microsOp)
  waitUntil (or (not (digitalReadOp ech)) (((microsOp) - start) > 23320))
  waitUntil (or (digitalReadOp ech) (((microsOp) - start) > 23320))
  if (((microsOp) - start) > 23320) {
    comment 'Distance sensor not ready; return the last distance reading'
    return _sr04_last
  }
  comment 'Pulse sent. Measure time until echo is detected.'
  start = (microsOp)
  waitUntil (or (not (digitalReadOp ech)) (((microsOp) - start) > 23320))
  _sr04_last = ((10 * ((microsOp) - start)) / 583)
  comment 'Leave some time for reverberations to die away.'
  waitMillis 10
  return _sr04_last
}

to sr04_distanceOnePin pin {
  return ('distance (cm)' pin pin)
}


module MueveCutebot
author 'por Pedro Ruiz'
version 1 0 
depends Cutebot 
choices cutebotmovimiento adelante 'atrás' parar izquierda derecha 
choices cutebotvelocidad '0' '1' '2' '3' '4' '5' '6' '7' '8' '9' '10' 
description ''

  spec ' ' 'adelante' 'acción _ _' 'menu.cutebotmovimiento menu.cutebotvelocidad' 'adelante' 0

to adelante movimiento velocidad {
  if (movimiento == 'adelante') {
    cutebot_setWheelSpeed ('[misc:rescale]' velocidad 0 10 0 100) ('[misc:rescale]' velocidad 0 10 0 100)
  } (movimiento == 'atrás') {
    cutebot_setWheelSpeed ('[misc:rescale]' velocidad 0 10 0 -100) ('[misc:rescale]' velocidad 0 10 0 -100)
  } (movimiento == 'izquierda') {
    cutebot_setWheelSpeed ('[misc:rescale]' velocidad 0 10 0 -100) ('[misc:rescale]' velocidad 0 10 0 100)
  } (movimiento == 'derecha') {
    cutebot_setWheelSpeed ('[misc:rescale]' velocidad 0 10 0 100) ('[misc:rescale]' velocidad 0 10 0 -100)
  } (movimiento == 'parar') {
    cutebot_setWheelSpeed ('[misc:rescale]' velocidad 0 10 0 0) ('[misc:rescale]' velocidad 0 10 0 0)
  }
}


module NeoPixel Output
author MicroBlocks
version 1 14 
description 'Control NeoPixel (WS2812) RGB LED strips and rings.
'
variables _np_pixels _np_pin _np_haswhite 

  spec ' ' 'neoPixelAttach' 'attach _ LED NeoPixel strip to pin _ : has white _' 'num auto bool' 10 '' false
  spec ' ' 'setNeoPixelColors10' 'set NeoPixels _ _ _ _ _ _ _ _ _ _' 'color color color color color color color color color color'
  spec ' ' 'setNeoPixelColors25' 'set NeoPixels #BR# _ _ _ _ _ #BR# _ _ _ _ _ #BR# _ _ _ _ _ #BR# _ _ _ _ _ #BR# _ _ _ _ _' 'color color color color color color color color color color color color color color color color color color color color color color color color color'
  spec ' ' 'clearNeoPixels' 'clear NeoPixels'
  spec ' ' 'neoPixelSetAllToColor' 'set all NeoPixels color _' 'color'
  spec ' ' 'setNeoPixelColor' 'set NeoPixel _ color _' 'num color' 1
  space
  spec 'r' 'neoPixel_colorSwatch' '_' 'color'
  spec 'r' 'colorFromRGB' 'color r _ g _ b _ (0-255)' 'num num num' 0 100 100
  spec 'r' 'randomColor' 'random color'
  space
  spec ' ' 'rotateNeoPixelsBy' 'rotate NeoPixels by _' 'auto' 1
  space
  spec ' ' 'NeoPixel_brighten' 'brighten NeoPixel _ by _' 'num num' 1 10
  spec ' ' 'NeoPixel_brighten_all' 'brighten all NeoPixels by _' 'num' 10
  spec ' ' 'NeoPixel_shift_color' 'shift NeoPixel _ color by _' 'num num' 1 10
  spec ' ' 'NeoPixel_shift_all_colors' 'shift all NeoPixel colors by _' 'num' 10
  space
  spec ' ' '_NeoPixel_ensureInitialized' '_NeoPixel_ensureInitialized'
  spec ' ' '_NeoPixel_increaseRGB' '_NeoPixel_increaseRGB of _ by _' 'num num' 1 10
  spec ' ' '_NeoPixel_rotate' '_NeoPixel_rotate_left _' 'bool' true
  spec ' ' '_NeoPixel_update' '_NeoPixel_update'
  spec ' ' '_NeoPixel_shift_hue' '_NeoPixel_shift_hue of _ by _' 'auto auto' '10' '10'

to NeoPixel_brighten i delta {
  '_NeoPixel_increaseRGB' i delta
  '_NeoPixel_update'
}

to NeoPixel_brighten_all delta {
  for i (size _np_pixels) {
    '_NeoPixel_increaseRGB' i delta
  }
  '_NeoPixel_update'
}

to NeoPixel_shift_all_colors delta {
  for i (size _np_pixels) {
    '_NeoPixel_shift_hue' i delta
  }
  '_NeoPixel_update'
}

to NeoPixel_shift_color i delta {
  '_NeoPixel_shift_hue' i delta
  '_NeoPixel_update'
}

to '_NeoPixel_ensureInitialized' {
  if (_np_pixels == 0) {if (or ((boardType) == 'M5Atom-Matrix') (or ((boardType) == 'Mbits') ((boardType) == 'micro:STEAMakers'))) {
    neoPixelAttach 25 '' false
  } ((boardType) == 'D1-Mini') {
    comment 'D1 mini kit'
    neoPixelAttach 7 15 false
  } ((boardType) == 'FOXBIT') {
    neoPixelAttach 35 '' false
  } ((boardType) == 'CodingBox') {
    neoPixelAttach 35 '' false
  } else {
    neoPixelAttach 10 '' false
  }}
}

to '_NeoPixel_increaseRGB' i delta {
  if (or (i < 1) (i > (size _np_pixels))) {return}
  local 'rgb' (at i _np_pixels)
  if (rgb != 0) {
    local 'h' ('[misc:hue]' rgb)
    local 's' ('[misc:saturation]' rgb)
    local 'v' (('[misc:brightness]' rgb) + delta)
    v = (maximum 20 (minimum v 100))
    atPut i _np_pixels ('[misc:hsvColor]' h s v)
  }
}

to '_NeoPixel_rotate' left {
  '_NeoPixel_ensureInitialized'
  local 'length' (size _np_pixels)
  if left {
    local 'first' (at 1 _np_pixels)
    for i (length - 1) {
      atPut i _np_pixels (at (i + 1) _np_pixels)
    }
    atPut length _np_pixels first
  } else {
    local 'last' (at length _np_pixels)
    for i (length - 1) {
      atPut ((length - i) + 1) _np_pixels (at (length - i) _np_pixels)
    }
    atPut 1 _np_pixels last
  }
}

to '_NeoPixel_shift_hue' i delta {
  if (or (i < 1) (i > (size _np_pixels))) {return}
  local 'rgb' (at i _np_pixels)
  if (rgb != 0) {
    local 'h' ((('[misc:hue]' rgb) + delta) % 360)
    local 's' ('[misc:saturation]' rgb)
    local 'v' ('[misc:brightness]' rgb)
    atPut i _np_pixels ('[misc:hsvColor]' h s v)
  }
}

to '_NeoPixel_update' {
  comment 'NeoPixel pin and hasWhite may have been changed by another library.'
  '[display:neoPixelSetPin]' _np_pin _np_hasWhite
  '[display:neoPixelSend]' _np_pixels
  waitMicros 300
}

to clearNeoPixels {
  '_NeoPixel_ensureInitialized'
  atPut 'all' _np_pixels 0
  '_NeoPixel_update'
}

to colorFromRGB r g b {
  r = (maximum 0 (minimum r 255))
  g = (maximum 0 (minimum g 255))
  b = (maximum 0 (minimum b 255))
  return (((r << 16) | (g << 8)) | b)
}

to neoPixelAttach number pinNumber optionalHasWhite {
  _np_pin = pinNumber
  _np_hasWhite = false
  if ((pushArgCount) > 2) {
    _np_hasWhite = optionalHasWhite
  }
  if (or (_np_pixels == 0) (number != (size _np_pixels))) {
    _np_pixels = (newList number)
  }
  atPut 'all' _np_pixels 0
  '[display:neoPixelSetPin]' _np_pin _np_hasWhite
}

to neoPixelSetAllToColor color {
  '_NeoPixel_ensureInitialized'
  atPut 'all' _np_pixels color
  '_NeoPixel_update'
}

to neoPixel_colorSwatch color {
  return color
}

to randomColor {
  local 'n1' (random 100 200)
  local 'n2' (random 0 100)
  if (1 == (random 1 3)) {
    return ((n1 << 16) | (n2 << 8))
  } (1 == (random 1 2)) {
    return ((n2 << 16) | n1)
  } else {
    return ((n1 << 8) | n2)
  }
}

to rotateNeoPixelsBy n {
  '_NeoPixel_ensureInitialized'
  local 'rotateLeft' (n < 0)
  if (or ((boardType) == 'CircuitPlayground') ((boardType) == 'CircuitPlayground Bluefruit')) {
    rotateLeft = (n > 0)
  }
  repeat (absoluteValue n) {
    '_NeoPixel_rotate' rotateLeft
  }
  '_NeoPixel_update'
}

to setNeoPixelColor i color {
  '_NeoPixel_ensureInitialized'
  if (and (1 <= i) (i <= (size _np_pixels))) {
    atPut i _np_pixels color
    '_NeoPixel_update'
  }
}

to setNeoPixelColors10 c1 c2 c3 c4 c5 c6 c7 c8 c9 c10 {
  '_NeoPixel_ensureInitialized'
  if ((size _np_pixels) >= 1) {
    atPut 1 _np_pixels c1
  }
  if ((size _np_pixels) >= 2) {
    atPut 2 _np_pixels c2
  }
  if ((size _np_pixels) >= 3) {
    atPut 3 _np_pixels c3
  }
  if ((size _np_pixels) >= 4) {
    atPut 4 _np_pixels c4
  }
  if ((size _np_pixels) >= 5) {
    atPut 5 _np_pixels c5
  }
  if ((size _np_pixels) >= 6) {
    atPut 6 _np_pixels c6
  }
  if ((size _np_pixels) >= 7) {
    atPut 7 _np_pixels c7
  }
  if ((size _np_pixels) >= 8) {
    atPut 8 _np_pixels c8
  }
  if ((size _np_pixels) >= 9) {
    atPut 9 _np_pixels c9
  }
  if ((size _np_pixels) >= 10) {
    atPut 10 _np_pixels c10
  }
  '_NeoPixel_update'
}

to setNeoPixelColors25 c1 c2 c3 c4 c5 c6 c7 c8 c9 c10 c11 c12 c13 c14 c15 c16 c17 c18 c19 c20 c21 c22 c23 c24 c25 {
  '_NeoPixel_ensureInitialized'
  if ((size _np_pixels) >= 1) {
    atPut 1 _np_pixels c1
  }
  if ((size _np_pixels) >= 2) {
    atPut 2 _np_pixels c2
  }
  if ((size _np_pixels) >= 3) {
    atPut 3 _np_pixels c3
  }
  if ((size _np_pixels) >= 4) {
    atPut 4 _np_pixels c4
  }
  if ((size _np_pixels) >= 5) {
    atPut 5 _np_pixels c5
  }
  if ((size _np_pixels) >= 6) {
    atPut 6 _np_pixels c6
  }
  if ((size _np_pixels) >= 7) {
    atPut 7 _np_pixels c7
  }
  if ((size _np_pixels) >= 8) {
    atPut 8 _np_pixels c8
  }
  if ((size _np_pixels) >= 9) {
    atPut 9 _np_pixels c9
  }
  if ((size _np_pixels) >= 10) {
    atPut 10 _np_pixels c10
  }
  if ((size _np_pixels) >= 11) {
    atPut 11 _np_pixels c11
  }
  if ((size _np_pixels) >= 12) {
    atPut 12 _np_pixels c12
  }
  if ((size _np_pixels) >= 13) {
    atPut 13 _np_pixels c13
  }
  if ((size _np_pixels) >= 14) {
    atPut 14 _np_pixels c14
  }
  if ((size _np_pixels) >= 15) {
    atPut 15 _np_pixels c15
  }
  if ((size _np_pixels) >= 16) {
    atPut 16 _np_pixels c16
  }
  if ((size _np_pixels) >= 17) {
    atPut 17 _np_pixels c17
  }
  if ((size _np_pixels) >= 18) {
    atPut 18 _np_pixels c18
  }
  if ((size _np_pixels) >= 19) {
    atPut 19 _np_pixels c19
  }
  if ((size _np_pixels) >= 20) {
    atPut 20 _np_pixels c20
  }
  if ((size _np_pixels) >= 21) {
    atPut 21 _np_pixels c21
  }
  if ((size _np_pixels) >= 22) {
    atPut 22 _np_pixels c22
  }
  if ((size _np_pixels) >= 23) {
    atPut 23 _np_pixels c23
  }
  if ((size _np_pixels) >= 24) {
    atPut 24 _np_pixels c24
  }
  if ((size _np_pixels) >= 25) {
    atPut 25 _np_pixels c25
  }
  '_NeoPixel_update'
}

